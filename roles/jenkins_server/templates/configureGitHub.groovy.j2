// 
// Configure Plugin - GitHub API
// 
// Configures the GitHub API so that Jenkins has the config and credentials it
// needs to create GitHub hooks, etc.
// 
// References:
// * https://gist.github.com/chrisvire/383a2c7b7cfb3f55df6a
// * http://stackoverflow.com/questions/32208763/update-jenkins-credentials-by-script


// These are the basic imports that Jenkin's interactive script console 
// automatically includes.
import jenkins.*;
import jenkins.model.*;
import hudson.*;
import hudson.model.*;


//
// Create/Update Credential for GitHub Access Token
//

def createOrUpdateStringCredentials = { id, description, secretValue ->
  domain = com.cloudbees.plugins.credentials.domains.Domain.global()
  credsStore = Jenkins.instance.getExtensionList("com.cloudbees.plugins.credentials.SystemCredentialsProvider")[0].getStore()
  
  secrets = com.cloudbees.plugins.credentials.CredentialsProvider.lookupCredentials(
    org.jenkinsci.plugins.plaincredentials.StringCredentials.class,
    Jenkins.instance
  )
  
  matchingSecret = secrets.find { secret -> secret.id == id }
  newSecret = new org.jenkinsci.plugins.plaincredentials.impl.StringCredentialsImpl(
    com.cloudbees.plugins.credentials.CredentialsScope.GLOBAL,
    id,
    description,
    hudson.util.Secret.fromString(secretValue)
  )
  
  if (matchingSecret) {
    println "Secret found: ${matchingSecret.id}"
    
    if (hudson.util.Secret.toString(newSecret.secret) != hudson.util.Secret.toString(matchingSecret.secret)) {
      updateResult = credsStore.updateCredentials(domain, matchingSecret, newSecret)  
      if (updateResult) {
        println "Changed: secret updated: ${id}" 
      } else {
        println "Secret failed to update: ${id}"
      }
    }
  } else {
    println "Secret not found: ${id}"
    credsStore.addCredentials(domain, newSecret)
    println "Changed: secret created: ${id}"
  }
}

def createOrUpdateUsernamePasswordCredentials = { id, description, username, password ->
  domain = com.cloudbees.plugins.credentials.domains.Domain.global()
  credsStore = Jenkins.instance.getExtensionList("com.cloudbees.plugins.credentials.SystemCredentialsProvider")[0].getStore()

  credentials = com.cloudbees.plugins.credentials.CredentialsProvider.lookupCredentials(
    com.cloudbees.plugins.credentials.common.StandardUsernamePasswordCredentials.class,
    Jenkins.instance
  )

  matchingCredential = credentials.find { credential -> credential.id == id }
  newCredential = new com.cloudbees.plugins.credentials.impl.UsernamePasswordCredentialsImpl(
    com.cloudbees.plugins.credentials.CredentialsScope.GLOBAL,
    id,
    description,
    username,
    password
  )

  if (matchingCredential) {
    println "Credential found: ${matchingCredential.id}"

    if (
        newCredential.scope != matchingCredential.scope ||
        newCredential.description != matchingCredential.description ||
        newCredential.username != matchingCredential.username ||
        hudson.util.Secret.toString(newCredential.password) != hudson.util.Secret.toString(matchingCredential.password)
    ) {
      updateResult = credsStore.updateCredentials(domain, matchingCredential, newCredential)
      if (updateResult) {
        println "Changed: credential updated: ${id}"
      } else {
        println "Credential failed to update: ${id}"
        throw new IllegalStateException()
      }
    }
  } else {
    println "Credential not found: ${id}"
    credsStore.addCredentials(domain, newCredential)
    println "Changed: credential created: ${id}"
  }
}

stringCredentialsId = 'github-token-string'
createOrUpdateStringCredentials(
  stringCredentialsId,
  "GitHub 'Personal Access Token' with the 'admin:repo_hook', 'repo:status', and 'repo_deployment' permissions.",
  "{{ vault_jenkins_github_token_secret }}"
)
usernamePasswordCredentialsId = 'github-token-username-password'
createOrUpdateUsernamePasswordCredentials(
  usernamePasswordCredentialsId,
  "GitHub 'Personal Access Token' with the 'admin:repo_hook', 'repo:status', and 'repo_deployment' permissions.",
  "{{ vault_jenkins_github_token_username }}",
  "{{ vault_jenkins_github_token_secret }}"
)


// 
// Configure GitHub API Plugin
// 

githubPluginConfig = Jenkins.instance.getDescriptor(org.jenkinsci.plugins.github.config.GitHubPluginConfig.class)

githubServerConfig = githubPluginConfig.configs.find { server -> server.credentialsId == stringCredentialsId }
if(githubServerConfig) {
  println("GitHub server config found: ${githubServerConfig}")
} else {
  println("GitHub server config not found.")
  githubPluginConfig.configs += new org.jenkinsci.plugins.github.config.GitHubServerConfig(stringCredentialsId)
  githubPluginConfig.save()
  println("Changed: GitHub server config added.")
}
